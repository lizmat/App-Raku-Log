#!/usr/bin/env raku

use v6.*;

use App::IRC::Log:ver<0.0.4>:auth<cpan:ELIZABETH>;
#use App::Raku::Log:ver<0.0.1>:auth<cpan:ELIZABETH>;
use IRC::Log::Colabti:ver<0.0.30>:auth<cpan:ELIZABETH>;
use Cro::HTTP::Server:ver<0.8.5>;

use lib 'lib';
use App::Raku::Log;

my constant $log-class = IRC::Log::Colabti;

sub MAIN(
  IO()  $log-dir      = 'logs',
  IO()  $base-dir     = "base",
  IO() :$rendered-dir = $base-dir.child('rendered'),
  IO() :$state-dir    = $base-dir.child('state'),
  IO() :$static-dir   = $base-dir.child('static'),
  IO() :$template-dir = $base-dir.child('template'),
  IO() :$zip-dir      = $base-dir.child('zip'),
       :$host         = 'localhost',
       :$port         = 10000,
       :$channels,
  ) {

    my @channels = $channels
      ?? $channels.split(",")
      !! App::IRC::Log.default-channels($log-dir);

    my $ail := App::IRC::Log.new:
      :$log-class, :$log-dir,
      :$rendered-dir, :$state-dir, :$static-dir, :$template-dir, :$zip-dir,
      colorize-nick => &colorize-nick,
      htmlize       => &htmlize,
      day-plugins   => day-plugins(),
      channels      => @channels,
    ;

    my $service := Cro::HTTP::Server.new:
      :application($ail.application),
      :$host, :$port,
    ;
    $service.start;

    react whenever signal(SIGINT) {
        $service.stop;
        $ail.shutdown;
        exit;
    }
}
